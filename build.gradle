group 'io.brain64bit.github'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

//inform intellij about generated code
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.plugin-shadow'

mainClassName = "io.github.brain64bit.SipServerWrapper"

buildscript {
    ext.kotlin_version = '1.2.0'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

def grpcVersion = '1.9.0' // CURRENT_GRPC_VERSION

dependencies {
    compile "com.google.api.grpc:proto-google-common-protos:0.1.22"
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"

    compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.2.6'

    compile "io.github.microutils:kotlin-logging:1.4.6"
    compile "org.slf4j:slf4j-simple:+"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile "io.grpc:grpc-testing:${grpcVersion}"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
    // change default generated file location from build to src
    generatedFilesBaseDir = "${projectDir}/src/generated"

    protoc {
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
        }
    }
}

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java");
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
    }
}

sourceSets {
    main {
        java {
            srcDirs 'src/generated/main/grpc'
            srcDirs 'src/generated/main/java'
        }
    }
}

startScripts.enabled = false

task sipServerWrapper(type: CreateStartScripts){
    mainClassName = "io.github.brain64bit"
    applicationName = "sip-server-wrapper"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(sipServerWrapper)
    fileMode = 0755
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes (
                'Main-Class': 'io.github.brain64bit.SipServerWrapperKt'
        )
    }

}

shadowJar {
    mergeServiceFiles()
}